package Torque::Log;
use Moose;
use DateTime::Format::Strptime;
use Torque::Log::Entry;
use namespace::autoclean;

=head1 NAME

Torque::Log

=head1 SYNOPSIS

  use Torque::Log;

  # Parse a whole file
  open my $f, '<', "logfile";
  my $logs = Torque::Log->new->parse($f);
  # $entry is an instance of Torque::Log::Entry
  my ($entry) = $logs->get_entries;

  # Or parse a single entry
  my ($entry) = Torque::Log->new->parse_line($log_file_line);

=head1 DESCRIPTION

Parse B<accounting> log entries generated by the Torque resource manager.

=head1 METHODS

=cut

has 'entries' => (
    is      => 'rw',
    isa     => 'ArrayRef[Torque::Log::Entry]',
    default => sub { [] },
);

=head2 dateparser( $format )

Instance of DateTime::Format::Strptime for parsing the time at which a log entry is from.

The default instance parses the following pattern:
  %m/%d/%Y %H:%M:%S

and will C<croak> if it can't parse dates in the log entries with this format.

=cut

has 'dateparser' => (
    is      => 'rw',
    isa     => 'DateTime::Format::Strptime',
    default => sub {
        DateTime::Format::Strptime->new(
            pattern  => '%m/%d/%Y %H:%M:%S',
            on_error => 'croak',
        );
    },
);

=head2 parse($filehandle)

Parses a log file read from $filehandle. Returns the Torque::Log instance.

=cut

sub parse {
    my ($self, $fh) = @_;
    while (my $line = <$fh>) {
        $self->parse_line($line);
    }
    return $self;
}

=head2 parse_line($log_line)

Parses a log file line from $log_line, returns the Torque::Log::Entry instance.

=cut

sub parse_line {
    my ($self, $line) = @_;

    chomp $line;
    my ($datetime, $type, $id, $info) = split /;/, $line, 4;

    my $dt = $self->dateparser->parse_datetime($datetime);

    my @info = split /\s+/, $info;
    my %info = map { split /=/, $_, 2 } @info;

    return $self->add_entry(
        Torque::Log::Entry->new(
            date => $dt,
            type => $type,
            id   => $id,
            info => \%info,
        )
    );
}

=head2 add_entry(@log_entries)

Adds instances of Torque::Log::Entry to the collection of log entries.

Returns these entries.

=cut

sub add_entry {
    my ($self, @entries) = @_;
    push @{ $self->entries }, @entries;
    return @entries;
}

=head2 get_entries()

Return all of the Torque::Log::Entry instances parsed by the object.

=cut

sub get_entries {
    my ($self) = @_;
    return @{ $self->entries };
}

=head1 SEE ALSO

L<Torque|http://www.adaptivecomputing.com/products/open-source/torque/>

L<PBS::Logs>

=head1 AUTHOR

Jonathan Barber - <jonathan.barber@gmail.com>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2013 Jonathan Barber

This library is free software; you can redistribute it and/or modify it under the same terms as Perl itself, either Perl version 5.8.1 or, at your option, any later version of Perl 5 you may have available.

=cut

__PACKAGE__->meta->make_immutable;

1;
